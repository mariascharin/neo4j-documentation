[role=enterprise-edition]
[[schema-index-fulltext]]
= Fulltext schema index

[abstract]
--
The fulltext schema indexes can be used to index nodes and relationships by string properties, and query by either exact or fuzzy matching.
--

Neo4j has had fulltext indexing capability for a long time via the explicit indexes, but these have a number of drawbacks, which are solved by the fulltext schema indexes.
For instance, the fulltext schema indexes are automatically populated with existing data from the store when they are created.
The legacy _auto indexes_, for instances, are only written to by transactions that occur while they are enabled.
The auto indexes are not populated with existing data when they are created.
Likewise, they will also miss updates that occur if they are temporarily disabled or misconfigured.

[[schema-index-fulltext-create-and-configure]]
== Create and configure fulltext schema indexes

Fulltext schema indexes are created with the `db.index.fulltext.createNodeIndex` and `db.index.fulltext.createRelationshipIndex`.
The indexes must each be given a unique name when created, which is used to reference the specific index in question, when querying or dropping an index.
A fulltext schema index then applies to a list of labels or a list of relationship types, for node and relationship indexes respectively, and then a list of property names.

An index that applies to more than one label, or more than one relationship type, is called a _multi-token index_.
Multi-token indexes can also apply to more than one property at a time, like a _composite index_ can, but with an important difference.
A composite index applies only to entities that match the indexed label and _all_ of the indexed properties.
A multi-token index, on the other hand, will index entities that have at least one of the indexed labels or relationship types, and _any_ of the indexed properties.

For instance, if we have a movie with a title.

.Query
[source, cypher]
----
CREATE (m:Movie { title: "The Matrix" })
RETURN m.title
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +m.title+
| +"The Matrix"+
1+d|1 row +
Nodes created: 1 +
Properties set: 1 +
Labels added: 1
|===

And we have a fulltext schema index on the `title` and `description` properties of movies and books.

.Query
[source, cypher]
----
CALL db.index.fulltext.createNodeIndex("titlesAndDescriptions",["Movie", "Book"],["title", "description"])
----

Then our movie node from above will be included in the index, even though it only have one of the indexed labels, and only one of the indexed properties:

.Query
[source, cypher]
----
CALL db.index.fulltext.queryNodes("titlesAndDescriptions", "matrix") YIELD node, score
RETURN node.title, node.description, score
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +node.title+ | +node.description+ | +score+
| +"The Matrix"+ | +<null>+ | +1.261009693145752+
3+d|1 row
|===

The same is true for fulltext schema indexes on relationships.
Though a relationship can only have one type, a relationship fulltext schema index can index multiple types, and all relationships will be included that match one of the relationship types, and at least one of the indexed properties.

The `db.index.fulltext.createNodeIndex` and `db.index.fulltext.createRelationshipIndex` takes an optional fourth argument, called `config`.
The `config` parameter is a map from string to string, and can be used to set index-specific configuration settings.
The `analyzer` setting can be used to configure an index-specific analyzer.
The possible values for the `analyzer` setting can be listed with the `db.index.fulltext.listAvailableAnalyzers` procedure.
The `eventually_consistent` setting, if set to `"true"`, will put the index in an _eventually consistent_ update mode.
this means that updates will be applied in a background thread "as soon as possible", instead of during transaction commit like other indexes.

[NOTE]
====
Using index-specific settings via the `config` parameter is experimental, because these settings currently do not get replicated in a clustered environment.
See the <<operations-manual#fulltext-index-configuration, Fulltext Schema Indexes>> section of the Operations Manual, for how to configure the default fulltext index configurations in <<file-locations, _neo4j.conf_>>.
====

.Query
[source, cypher]
----
CALL db.index.fulltext.createRelationshipIndex("taggedByRelationshipIndex",["TAGGED_AS"],["taggedByUser"], { analyzer: "url_or_email", eventually_consistent: "true" })
----

In this example, an eventually consistent relationship fulltext schema index is created for the `TAGGED_AS` relationship type, and the `taggedByUser` property, and the index uses the `url_or_email` analyzer.
This could, for instance, be a system where people are assigning tags to documents, and where the index on the `taggedByUser` property will allow them to quickly find all of the documents they have tagged.
Had it not been for the relationship index, one would have had to add artificial connective nodes between the tags and the documents in the data model, just so these nodes could be indexed.

.Result
[role="queryresult",options="footer",cols="1*<m"]
|===
1+|(empty result)
1+d|0 rows
|===

[[schema-index-fulltext-query]]
== Query fulltext schema indexes

Fulltext indexes will, in addition to any exact matches, also return _approximate_ matches to a given query.
Both the property values that are indexed, and the queries to the index, are processed through the analyzer such that the index can find that don't _exactly_ matches.
The `score` that is returned alongside each result entry, represents how well the index thinks that entry matches the given query.
The results are always returned in _descending score order_, where the best matching result entry is put first.
To illustrate, in the example below, we search our movie database for "Full Metal Jacket", and even though there is an exact match as the first result, we also get three other less interesting results:

.Query
[source, cypher]
----
CALL db.index.fulltext.queryNodes("titlesAndDescriptions", "Full Metal Jacket") YIELD node, score
RETURN node.title, score
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +node.title+ | +score+
| +"Full Metal Jacket"+ | +0.8093575239181519+
| +"The Jacket"+ | +0.1152719184756279+
| +"Full Moon High"+ | +0.0836455449461937+
| +"Yellow Jacket"+ | +0.07204495370388031+
2+d|4 rows
|===

The fulltext schema indexes uses Lucene under the hood.
This means that we can use Lucene's fulltext query language to express what we wish to search for.
For instance, if we are only interested in exact matches, then we can quote the string we are searching for.

.Query
[source, cypher]
----
CALL db.index.fulltext.queryNodes("titlesAndDescriptions", "\"Full Metal Jacket\"") YIELD node, score
RETURN node.title, score
----

When we put "Full Metal Jacket" in quotes, Lucene only gives us exact matches.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +node.title+ | +score+
| +"Full Metal Jacket"+ | +1.3701786994934082+
2+d|1 row
|===

Lucene also allows us to use logical operators, such as `AND` and `OR`, to search for terms:

.Query
[source, cypher]
----
CALL db.index.fulltext.queryNodes("titlesAndDescriptions", 'full AND metal') YIELD node, score
RETURN node.title, score
----

Only the "Full Metal Jacket" movie in our database has both the words "full" and "metal".

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +node.title+ | +score+
| +"Full Metal Jacket"+ | +0.7603841423988342+
2+d|1 row
|===

It is also possible to search for only specific properties, by putting the property name and a colon in front of the text being searched for.

.Query
[source, cypher]
----
CALL db.index.fulltext.queryNodes("titlesAndDescriptions", 'description:"surreal adventure"') YIELD node, score
RETURN node.title, node.description, score
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +node.title+ | +node.description+ | +score+
| +"Metallica Through The Never"+ | +"The movie follows the young roadie Trip through his surreal adventure with the band."+ | +1.311632513999939+
3+d|1 row
|===

A complete description of the Lucene query syntax can be found in the http://lucene.apache.org/core/5_5_0/queryparser/org/apache/lucene/queryparser/classic/package-summary.html#package.description[Lucene documentation].
